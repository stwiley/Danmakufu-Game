#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Mother Spell 4 Single"]
#Text["Code by Cyvo[r]Mother Spell 4"]
#include "script/default_system/Default_ShotConst.txt"
#include "./../lib/lib_general.dnh"
#Background["./../system/Default_Background_IceMountain.txt"]
#System["./../system/Default_System.txt"]


let objBoss;
let objScene= GetEnemyBossSceneObjectID();


@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){
        SetScriptResult(1000);
    }
    case(EV_REQUEST_TIMER){
        SetScriptResult(60);
    }
    case(EV_REQUEST_SPELL_SCORE){
        SetScriptResult(1000000);
    }
    
}

@Initialize{
    objBoss= ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
    ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY(), 60);
    SetPauseScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_Pause.txt");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_EndScene.txt");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_ReplaySaveScene.txt");
    
    //ObjEnemyBossScene_StartSpell(objScene);
    
    TDrawLoop;
    TFinalize;
    MainTask;
}

@MainLoop{
    ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
    yield;
}

task TFinalize{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){yield;}
    NotifyEventAll(EV_USER_STAGE+2, [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)]);
    Obj_Delete(objBoss);
    DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
    SetAutoDeleteObject(true);
    CloseScript(GetOwnScriptID());
    return;
}

task MainTask{
    wait(120);
    chaosStorm;
    needleShot;
}

task chaosStorm{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        let randX= rand(0, GetStgFrameWidth());
        let randY= rand(0, GetStgFrameHeight());
        let hail1= CreateShotA1(randX, 0, 3, 135, rand(225, 232), 5);
        let hail2= CreateShotA1(GetStgFrameWidth(), randY, 3, 135, rand_int(225, 232), 5);
        snaking(hail1);
        snaking(hail2);
        wait(10);
    }
}

task snaking(obj){
    let wvel=1;
    ascent (i in 0..12){
        ObjMove_AddPatternA2(obj, i*30, NO_CHANGE, NO_CHANGE, rand(0.05, 0.2), wvel, 3);
        wvel *= -1;
    }
}

task needleShot{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        let needle = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 4, GetAngleToPlayer(objBoss), 129, 5);
        needleTrail(needle);
        wait(120);
    }
}

task needleTrail(needle){
    while(needle){
        if(Obj_IsDeleted(needle)){return;}
        CreateShotA2(ObjMove_GetX(needle), ObjMove_GetY(needle), 1, rand(0, 360), 0.01, 3, rand_int(121, 128), 5);
        wait(10);
    }
}

task TDrawLoop{
    let BossImage = GetCurrentScriptDirectory() ~ "./../img/Mother.png"; //assumes that image is in same directory
    //let animframe = 0;
    ObjPrim_SetTexture(objBoss, BossImage);
    ObjRender_SetScaleXYZ(objBoss, 1, 1, 1);
    ObjSprite2D_SetSourceRect(objBoss, 0, 0, 96, 96);
    ObjSprite2D_SetDestCenter(objBoss);
    /*while(!Obj_IsDeleted(objBoss)){
        ObjSprite2D_SetSourceRect(objBoss, 0 + 64*floor(animframe/8), 0, 64 + 64*floor(animframe/8), 64);
        animframe++;
        if(animframe >= 32){animframe = 0;}//reset if too high
        yield;
    }*/
}