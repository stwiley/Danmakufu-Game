#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Victoria Spell 4 Single"]
#Text["Code by Cyvo[r]Victoria Spell 4"]
#include "script/default_system/Default_ShotConst.txt"
#include "./../lib/lib_general.dnh"
#Background["./../system/Default_Background_IceMountain.txt"]
#System["./../system/Default_System.txt"]


let objBoss;
let objScene= GetEnemyBossSceneObjectID();


@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){
        SetScriptResult(1000);
    }
    case(EV_REQUEST_TIMER){
        SetScriptResult(60);
    }
    case(EV_REQUEST_SPELL_SCORE){
        SetScriptResult(1000000);
    }
    
}

@Initialize{
    objBoss= ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
    ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);
    SetPauseScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_Pause.txt");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_EndScene.txt");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_ReplaySaveScene.txt");
    
    ObjEnemyBossScene_StartSpell(objScene);
    
    TDrawLoop;
    TFinalize;
    MainTask;
}

@MainLoop{
    ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
    yield;
}

task TFinalize{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){yield;}
    NotifyEventAll(EV_USER_STAGE+2, [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)]);
    Obj_Delete(objBoss);
    DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
    SetAutoDeleteObject(true);
    CloseScript(GetOwnScriptID());
    return;
}

task MainTask{
    movement;
    wait(120);
    spinLaser;
    otherShots;

}

task spinLaser{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        let angleT=0;
        let count= 0;
        loop{
            loop(12){
                if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}
                CreateStraightLaserA1(ObjMove_GetX(objBoss)+30*cos(angleT), ObjMove_GetY(objBoss)+30*sin(angleT), angleT, 512, 30, 60, 153, 30);
                angleT += 360/12;
            }
            angleT += sin(count)*6;
            count+=20;
            wait(30);
        }

    }
}

task otherShots{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        let angleT= rand(0, 360);
        loop(20){
            let rad= CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 5, angleT, -0.05, 1, 65, 5);
            angleT += 360/20;

        }
        wait(30);
    }
}

task TDrawLoop{
    let BossImage = GetCurrentScriptDirectory() ~ "./../img/u2l13spritesheet.png"; //assumes that image is in same directory
    let animframe = 0;
    ObjPrim_SetTexture(objBoss, BossImage);
    ObjRender_SetScaleXYZ(objBoss, 1, 1, 1);
    ObjSprite2D_SetSourceRect(objBoss, 0, 0, 64, 64);
    ObjSprite2D_SetDestCenter(objBoss);
    while(!Obj_IsDeleted(objBoss)){
        ObjSprite2D_SetSourceRect(objBoss, 0 + 64*floor(animframe/8), 0, 64 + 64*floor(animframe/8), 64);
        animframe++;
        if(animframe >= 32){animframe = 0;}//reset if too high
        yield;
    }
}

task movement{
    ObjMove_SetDestAtFrame(objBoss, GetStgFrameWidth()/2, 60, 60);
}