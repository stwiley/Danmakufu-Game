#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Victoria Spell 2 Single"]
#Text["Code by Cyvo[r]Victoria Spell 2"]
#include "script/default_system/Default_ShotConst.txt"
#include "./../lib/lib_general.dnh"
#Background["./../system/Default_Background_IceMountain.txt"]
#System["./../system/Default_System.txt"]


let objBoss;
let objScene= GetEnemyBossSceneObjectID();


@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){
        SetScriptResult(1000);
    }
    case(EV_REQUEST_TIMER){
        SetScriptResult(60);
    }
    case(EV_REQUEST_SPELL_SCORE){
        SetScriptResult(1000000);
    }
    
}

@Initialize{
    objBoss= ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
    ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);
    SetPauseScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_Pause.txt");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_EndScene.txt");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_ReplaySaveScene.txt");
    
    ObjEnemyBossScene_StartSpell(objScene);
    
    TDrawLoop;
    TFinalize;
    MainTask;
}

@MainLoop{
    ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
    yield;
}

task TFinalize{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){yield;}
    NotifyEventAll(EV_USER_STAGE+2, [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)]);
    Obj_Delete(objBoss);
    DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
    SetAutoDeleteObject(true);
    CloseScript(GetOwnScriptID());
    return;
}

task MainTask{
    wait(120);
    bigCircle;
    bombShell;
}

task bigCircle{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        let angleT = 0;
        loop(20){
            let obj=CreateShotA1(ObjMove_GetX(objBoss)+60*cos(angleT), ObjMove_GetY(objBoss)+60*sin(angleT), 2, angleT, rand_int(17, 24), 5);
            angleT +=180/20;
            ObjMove_SetAngularVelocity(obj, 0.2);
            yield;
        }
        angleT=0;
        yield;
        loop(20){
            let obj2=CreateShotA1(ObjMove_GetX(objBoss)+60*-cos(angleT), ObjMove_GetY(objBoss)+60*-sin(angleT), 2, angleT + 180, rand_int(17, 24), 5);
            angleT -=180/20;
            ObjMove_SetAngularVelocity(obj2, -0.2);
            yield;
        }
        wait(20);
    }
}

task bombShell{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){
        let RShellSpeedX = rand(-3, 3);
        let RShellAccX= rand(-0.1, 0.1);
        CreateShotB2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 0, -3, RShellAccX, 0.1, RShellSpeedX, 3, rand_int(165, 172), 5);
        wait(15);
    }
}

task TDrawLoop{
    let BossImage = GetCurrentScriptDirectory() ~ "./../img/u2l13spritesheet.png"; //assumes that image is in same directory
    let animframe = 0;
    ObjPrim_SetTexture(objBoss, BossImage);
    ObjRender_SetScaleXYZ(objBoss, 1, 1, 1);
    ObjSprite2D_SetSourceRect(objBoss, 0, 0, 64, 64);
    ObjSprite2D_SetDestCenter(objBoss);
    while(!Obj_IsDeleted(objBoss)){
        ObjSprite2D_SetSourceRect(objBoss, 0 + 64*floor(animframe/8), 0, 64 + 64*floor(animframe/8), 64);
        animframe++;
        if(animframe >= 32){animframe = 0;}//reset if too high
        yield;
    }
}