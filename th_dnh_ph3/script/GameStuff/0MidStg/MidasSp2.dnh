#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Midas Spell 2 Single"]
#Text["Code by Cyvo[r]Midas Spell 2"]
#include "script/default_system/Default_ShotConst.txt"
#include "./../lib/lib_general.dnh"
#Background["./../system/Default_Background_IceMountain.txt"]
#System["./../system/Default_System.txt"]


let objBoss;
let objScene= GetEnemyBossSceneObjectID();


@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){
        SetScriptResult(1000);
    }
    case(EV_REQUEST_TIMER){
        SetScriptResult(60);
    }
    case(EV_REQUEST_SPELL_SCORE){
        SetScriptResult(1000000);
    }
    
}

@Initialize{
    objBoss= ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
    ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);
    SetPauseScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_Pause.txt");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_EndScene.txt");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_ReplaySaveScene.txt");
    
    ObjEnemyBossScene_StartSpell(objScene);
    
    TDrawLoop;
    TFinalize;
    MainTask;
}

@MainLoop{
    ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
    yield;
}

task TFinalize{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){yield;}
    NotifyEventAll(EV_USER_STAGE+2, [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)]);
    Obj_Delete(objBoss);
    DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
    SetAutoDeleteObject(true);
    CloseScript(GetOwnScriptID());
    return;
}

task MainTask{
    wait(120);
    movement;
    midasRadial;
    laserWalls;
}

task midasRadial{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
      let angleT = rand(0, 360);
      loop(12){
        loop(20){
            let radShot= CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 2, angleT, rand(181, 184), 5);
            angleT += 360/20;
        }
        wait(30);
      }
      wait(120);
    }
}

task laserWalls{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        let shotArray2 = [56, 112, 168, 220, 276, 332, 384, 436];
        let shotArray3 = [412, 360, 304, 248, 196, 140, 84, 28];
        ascent(i in 0..7){
            let crowLas1 = CreateStraightLaserA1(10, shotArray2[i], 0, 500, 20, 30, 153, 30);
            wait(30);
        }
        yield;
        ascent(i in 0..7){
            let crowLas2 = CreateStraightLaserA1(GetStgFrameWidth()-10, shotArray3[i], 180, 500, 20, 30, 153, 30);
            wait(30);
        }
        yield;
    }
}

task TDrawLoop{
    let BossImage = GetCurrentScriptDirectory() ~ "./../img/u2l13spritesheet.png"; //assumes that image is in same directory
    let animframe = 0;
    ObjPrim_SetTexture(objBoss, BossImage);
    ObjRender_SetScaleXYZ(objBoss, 1, 1, 1);
    ObjSprite2D_SetSourceRect(objBoss, 0, 0, 64, 64);
    ObjSprite2D_SetDestCenter(objBoss);
    while(!Obj_IsDeleted(objBoss)){
        ObjSprite2D_SetSourceRect(objBoss, 0 + 64*floor(animframe/8), 0, 64 + 64*floor(animframe/8), 64);
        animframe++;
        if(animframe >= 32){animframe = 0;}//reset if too high
        yield;
    }
}

task movement{
    wait(60);
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        ObjMove_SetDestAtFrame(objBoss, rand(GetCenterX() + 90, GetCenterX()-90), rand(GetCenterY()+90, GetCenterY()-90), 60);
        wait(120);
    }
}