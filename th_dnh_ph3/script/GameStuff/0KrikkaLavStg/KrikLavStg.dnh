#TouhouDanmakufu[Stage]
#ScriptVersion[3]
#Title["Krikka/Lav Stage"]
#Text["Code by Cyvo[r]Krikka and Lavarus stage"]
#include "./../lib/lib_general.dnh"
#include"./../lib/lib_dialogue.txt"
#include "./../stgenemy/StageEnemy1.txt"
#Background["./../system/Background_Junk.txt"]
#System["./../system/Default_System.txt"]

let stagepart = 0;
let bgm;
let bgm2;
let dir = GetCurrentScriptDirectory();

@Event{
    alternative(GetEventType())
    case(EV_PAUSE_ENTER){
        if(stagepart == 0){
            ObjSound_Stop(bgm);
        }
        if(stagepart == 1){
            ObjSound_Stop(bgm2);
        }
    }
    case(EV_PAUSE_LEAVE){
        if(stagepart == 0){
            ObjSound_Play(bgm);
        }
        if(stagepart == 1){
            ObjSound_Play(bgm2);
        }
    }
    case(EV_USER_STAGE+1){
        let arg = GetEventArgument(0);
        CreateItemA1(ITEM_POINT, arg[0], arg[1], 15000);
    }
    case(EV_USER_STAGE+2){
        let arg = GetEventArgument(0);
        ascent(i in 0..12){
            CreateItemA1(ITEM_POINT, arg[0] + 30*cos(30*i), arg[1] + 30*sin(30*i), 15000);
        }
    }
}

@Initialize{
    SetPauseScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_Pause.txt");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_EndScene.txt");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_ReplaySaveScene.txt");
    TStage;
}

@MainLoop{
    yield;
}

task TStage{
    let pathmidboss = dir ~ "LavPlural.dnh";
    let pathboss = dir ~ "KrikkaPlural.dnh";
    let midbossScript = LoadScriptInThread(pathmidboss);
    let bossScript = LoadScriptInThread(pathboss);
    
    SetForbidPlayerSpell(true);
    SetForbidPlayerShot(true);
    SetPlayerSpeed(0,0);
    SetStageDifficulty();
    
    bgm2 = ObjSound_Create();
    ObjSound_Load(bgm2, dir ~ "./../img/metacrys.ogg");
    StageBGM(bgm2, 2);
    
    bgm= ObjSound_Create();
    ObjSound_Load(bgm, dir~ "./../img/whatuneed.ogg");
    StageBGM(bgm, 1);
    
    ObjSound_Play(bgm);

    loop (8){
		CreateStageEnemy1(12, 12, 2, 45);
		wait(30);
	}
    
    
    NoEnemy();

    StartScript(midbossScript);
    BossName();
    while(!IsCloseScript(midbossScript) && GetPlayerState() != STATE_END){
        yield;
    }
    
    loop(120){
        yield;
    }
    
    if(GetPlayerState() != STATE_END){
    	StageEvent();
    }else{
    	CloseStgScene();
    }    
    
    loop(120){yield;}
    
    StartScript(bossScript);
    stagepart = 1;
    BossName;
    ObjSound_Stop(bgm);
    ObjSound_Play(bgm2);
    
    while(!IsCloseScript(bossScript) && GetPlayerState() != STATE_END){
        yield;
    }
    
    loop(240){
        yield;
    }
    CloseStgScene();
}

function StageBGM(obj, ID){
    ObjSound_SetSoundDivision(obj, SOUND_BGM);
    ObjSound_SetRestartEnable(obj, true);
    ObjSound_SetLoopEnable(obj, true);
    ObjSound_SetLoopTime(obj, 0, 300);
    if(ID == 1){
        ObjSound_SetLoopTime(obj, 0, 299);
        ObjSound_SetVolumeRate(obj, 10);
    }
    if(ID == 2){
        ObjSound_SetLoopTime(obj, 0, 166);
        ObjSound_SetVolumeRate(obj, 10);
        
    }
    return obj;
}

function SetStageDifficulty{
    let selection =0;
    let sdiff = 0;
    let TextObjects = [];
    let TextNames = ["Normal", "Hard"];
    
    let diffs = [1,2];
    
    let Center = [GetStgFrameWidth()/2, GetStgFrameHeight/2];
    ascent(i in 0..length(TextNames)){
        TextObjects = TextObjects ~ [DrawSubMenuText(TextNames[i])];
        let locText = TextObjects[i];
        ObjText_SetHorizontalAlignment(locText, ALIGNMENT_CENTER);
        ObjRender_SetPosition(locText, Center[0] - 100, Center[1] - 150 + i*25, 0);
        ObjText_SetMaxWidth(locText, 200);
        ObjText_SetFontSize(locText, 16);
        ObjText_SetFontType(locText, "Helvetica");
        ObjText_SetFontBorderColor(locText, 16, 16, 16);
    }
    
    loop{
        if(GetVirtualKeyState(VK_UP) == KEY_PUSH){selection--;}
        if(GetVirtualKeyState(VK_DOWN) == KEY_PUSH){selection++;}
        if(selection < 0){selection = 1;}
        if(selection > 1){selection = 0;}
        
        ascent(i in 0..length(TextObjects)){
            if (selection == i){
                ObjText_SetFontColorTop(TextObjects[i], 255, 127, 40);
                ObjText_SetFontColorBottom(TextObjects[i], 255/1.5, 127/1.5, 40/1.5);
            } else{
                ObjText_SetFontColorTop(TextObjects[i], 255/1.75, 127/1.75, 40/1.75);
                ObjText_SetFontColorBottom(TextObjects[i], 255/2.25, 127/2.25, 40/2.25);
            }
        }
        
        if(GetVirtualKeyState(VK_SHOT) == KEY_PUSH){
            sdiff = diffs[selection];
            SetCommonData("Difficulty", sdiff);
            break;
        }
        
        yield;
    }
    
    SetForbidPlayerSpell(false);
    SetPlayerSpeed(4,1.6);
    
    ascent(i in 0..length(TextObjects)){
        Obj_Delete(TextObjects[i]);
    }
    wait(60);
    SetForbidPlayerShot(false);
}

function DrawSubMenuText(string){
    let objMText = ObjText_Create();
    ObjText_SetText(objMText, string);
    ObjText_SetFontSize(objMText, 16);
    ObjText_SetFontType(objMText, "Helvetica");
    
    return objMText;
}

function StageEvent{
    let dialogueobj = CreateDialogueEvent();
    let textobj = dialogueobj[1];
    let leftportrait = dialogueobj[2];
    let rightportrait = dialogueobj[3];
    
    SetForbidPlayerSpell(true);
    SetForbidPlayerShot(true);
    
    let pathKrikka= GetCurrentScriptDirectory() ~ "./../lib/newkrikkarough.png";
    let pathNigma= GetCurrentScriptDirectory() ~ "./../lib/newnigmarough.png";
    
    ObjPrim_SetTexture(leftportrait, pathNigma);
    ObjPrim_SetTexture(rightportrait, pathKrikka);
    
    ObjText_SetText(textobj, "Another one? You gonna tell me where I am, [r]or am I gonna hafta blast you too?");
    ObjText_SetFontBorderColor(textobj, 255, 128, 192);
    SetPortraitToForeground(leftportrait);
    SetPortraitToBackground(rightportrait);
    EventWaitA3(15);
    
    ObjText_SetText(textobj, "Another one?! [r]My name's Krikka, and you're in the Expanse.");
    ObjText_SetFontBorderColor(textobj, 255, 128, 128);
    SetPortraitToForeground(rightportrait);
    SetPortraitToBackground(leftportrait);
    EventWaitA1(15);
    
    ObjText_SetText(textobj, "The Expanse?! So the teleporter---");
    ObjText_SetFontBorderColor(textobj, 255, 128, 192);
    SetPortraitToForeground(leftportrait);
    SetPortraitToBackground(rightportrait);
    EventWaitA1(15);
    
    ObjText_SetText(textobj, "Looks like it. Right now though, you're on my [r]shit list. I saw what you did to Lavvy back there.");
    ObjText_SetFontBorderColor(textobj, 255, 128, 128);
    SetPortraitToForeground(rightportrait);
    SetPortraitToBackground(leftportrait);
    EventWaitA1(15);
    
    ObjText_SetText(textobj, "That thing was Lavvy? Your friend attacked ME first!");
    ObjText_SetFontBorderColor(textobj, 255, 128, 192);
    SetPortraitToForeground(leftportrait);
    SetPortraitToBackground(rightportrait);
    EventWaitA1(15);
    
    ObjText_SetText(textobj, "Do I look like I care, fleshling? You beat up my friend, [r]so now I gotta return the favor.");
    ObjText_SetFontBorderColor(textobj, 255, 128, 128);
    SetPortraitToForeground(rightportrait);
    SetPortraitToBackground(leftportrait);
    EventWaitA1(15);

    ObjText_SetText(textobj, "I'm already sensing a pattern with you guys... Fine! Let's go!");
    ObjText_SetFontBorderColor(textobj, 255, 128, 192);
    SetPortraitToForeground(leftportrait);
    SetPortraitToBackground(rightportrait);
    EventWaitA1(15);
    
    DestroyDialogueEvent(dialogueobj);
    SetForbidPlayerSpell(false);
    SetForbidPlayerShot(false);
}

task BossName{
    let objText1 = ObjText_Create();
    let objText2 = ObjText_Create();
	ObjText_SetFontSize(objText1, 16);
    ObjText_SetFontSize(objText2, 16);
	ObjText_SetMaxWidth(objText1, 100);
    ObjText_SetMaxWidth(objText2, 100);
	ObjRender_SetY(objText1,15);
    ObjRender_SetY(objText2,15);
    Obj_SetVisible(objText2, false);
    ObjText_SetText(objText1, "Lavarus");
    ObjText_SetText(objText2, "Krikka");
    if(stagepart == 1){
        Obj_SetVisible(objText1, false);
        Obj_SetVisible(objText2, true);
    }
}