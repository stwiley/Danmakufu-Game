#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Krikka Spell 2 Single"]
#Text["Code by Cyvo[r]Krikka Spell 2"]
#include "script/default_system/Default_ShotConst.txt"
#include "./../lib/lib_general.dnh"
#Background["./../../tutorial/system/Default_Background_IceMountain.txt"]
#System["./../../tutorial/system/Default_System.txt"]


let objBoss;
let objScene= GetEnemyBossSceneObjectID();


@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){
        SetScriptResult(1000);
    }
    case(EV_REQUEST_TIMER){
        SetScriptResult(60);
    }
    case(EV_REQUEST_SPELL_SCORE){
        SetScriptResult(1000000);
    }
    
}

@Initialize{
    objBoss= ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
    ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);
    SetPauseScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_Pause.txt");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_EndScene.txt");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "./../system/Default_ReplaySaveScene.txt");
    
    //ObjEnemyBossScene_StartSpell(objScene);
    
    TDrawLoop;
    TFinalize;
    MainTask;
}

@MainLoop{
    ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
    yield;
}

task TFinalize{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){yield;}
    NotifyEventAll(EV_USER_STAGE+2, [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)]);
    Obj_Delete(objBoss);
    DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
    SetAutoDeleteObject(true);
    CloseScript(GetOwnScriptID());
    return;
}

task MainTask{
    wait(120);
    FiveCone;
    ThreeCones;
}

task FiveCone{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
		let angleT= 0;
		let playerAngle = GetAngleToPlayer(objBoss);
        CreateShotA1(ObjMove_GetX(objBoss)+cos(angleT), ObjMove_GetY(objBoss)+sin(angleT), 4, angleT + playerAngle, DS_BALL_BS_GREEN, 5);
		loop(2){
        	angleT += 45/5;
			CreateShotA1(ObjMove_GetX(objBoss)+cos(angleT), ObjMove_GetY(objBoss)+sin(angleT), 4, angleT + playerAngle, DS_BALL_BS_GREEN, 5);
            CreateShotA1(ObjMove_GetX(objBoss)+cos(angleT), ObjMove_GetY(objBoss)+sin(angleT), 4, -angleT + playerAngle, DS_BALL_BS_GREEN, 5);
		}
		wait(30);
    }
}
task ThreeCones{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) >0){
        let angleT=0;
        loop(12){
            ascent(i in 0..4){
                CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 4.5-i, angleT, 75+i, 0);
            }
            angleT += 200/12;           
        }
        wait(60);
    }
}

task TDrawLoop{
    let BossImage = GetCurrentScriptDirectory() ~ "./../img/u2l13spritesheet.png"; //assumes that image is in same directory
    let animframe = 0;
    ObjPrim_SetTexture(objBoss, BossImage);
    ObjRender_SetScaleXYZ(objBoss, 1, 1, 1);
    ObjSprite2D_SetSourceRect(objBoss, 0, 0, 64, 64);
    ObjSprite2D_SetDestCenter(objBoss);
    while(!Obj_IsDeleted(objBoss)){
        ObjSprite2D_SetSourceRect(objBoss, 0 + 64*floor(animframe/8), 0, 64 + 64*floor(animframe/8), 64);
        animframe++;
        if(animframe >= 32){animframe = 0;}//reset if too high
        yield;
    }
}